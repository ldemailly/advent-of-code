// Day 2 of advent of code 2025 - grol version
// https://adventofcode.com/2025/day/2

func readInput() {
    res := []
    line := read() // single line
    for i := split(line, ",") {
        iv := split(i, "-")
        start := int(iv[0])
        end := int(iv[1])
        res += [[start, end]]
    }
    return res
}

func isInvalid(n) {
    s := str(n)
    if len(s) % 2 != 0 {
        return false
    }
    return s[0:len(s)/2] == s[len(s)/2:len(s)]
}

println("Part 1:")
input := readInput()
println("input:", input)

sum := 0
for ivals := input {
    start := ivals[0]
    end := ivals[1]+1
    count := 0
    for i:=start:end {
        if isInvalid(i) {
            println("  1.Invalid number found:", i)
            sum+=i
        }
    }
}
println("1.Result:", sum)

func cutByN(str, n) {
    res := []
    l := len(str)
    for i := 0:l:n {
        res += str[i:i+n]
    }
    return res
}

func allEqual(arr) {
    f := arr[0]
    for i:= 1:len(arr) {
        if arr[i] != f {
            return false
        }
    }
    return true
}

func part2Invalid(n) {
    s := str(n)
    l := len(s)
    last := l/2+1
    for i := 1:last {
        if l % i != 0 {
            // println("  2.Skipping length", i, "for number", n)
            continue
        }
        // println("  2.Checking length", i, "for number", n)
        if allEqual(cutByN(s, i)) {
            return true
        }
    }
    return false
}


sum := 0
for ivals := input {
    start := ivals[0]
    end := ivals[1]+1
    for i:=start:end {
        if part2Invalid(i) {
            println("  2.Invalid number found:", i)
            sum+=i
        }
    }
}
println("2.Result:", sum)
