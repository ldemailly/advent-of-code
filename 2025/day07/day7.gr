// Day 7 of advent of code 2025 - grol version
// https://adventofcode.com/2025/day/7



func readInput() {
    start := -1
    for !eof() {
        line := read()
        if line == "" {
            continue
        }
        row := []
        i:=0
        for vs := split(line) {
            v := 0
            if (vs == "^") {
                v = 2 // 2 for splitter
            } else if (vs == "S") {
                v = 1 // for start
                res := [] // in case S is not at the first line
                start = i
            }
            row += v
            i++
        }
        res += [row]
    }
    return [start,res]
}

func printGrid(grid) {
    w := len(grid[0])
    h := len(grid)
    for y := 0:h {
        line := ""
        for x := w {
            v := grid[y][x]
            if v == 0 {
                line += " "
            } else if v == 1 {
                line += "S"
            } else if v == 2 {
                line += "^"
            } else {
                line += "|"
            }
        }
        println(line)
    }
}

func copy(row) {
    nrow := []
    for v := row {
        nrow += v
    }
    return nrow
}

func step(row, beams) {
    nbeams := {}
    hit := 0
    nrow := copy(row)
    for bm := beams {
        b := bm.key
        if row[b] == 2 {
            // splitter
            nbeams[b-1] = true
            nbeams[b+1] = true
            nrow[b-1] = 3
            nrow[b+1] = 3
            hit++
        } else {
            nrow[b] = 3
            nbeams[b] = true
        }
    }
    return [nbeams, nrow, hit]
}

input := readInput()
start := input[0]
grid := input[1]
println("Initial...  start at x=", start)
printGrid(grid)

h := len(grid)
beams := {start: true}
sum := 0
ngrid := []
for y := 1:h-1 {
    res := step(grid[y], beams)
    beams = res[0]
    ngrid += [res[1]]
    hit := res[2]
    sum += hit
    println("At row ", y, " splitters hit=", hit, " total so far=", sum)
    printGrid(ngrid)
}
println("Part 1: Total splitters hit:", sum)


func step2(row, beams) {
    nbeams := {}
    func incr(key, v) {
        if nbeams[key] == nil {
            nbeams[key] = 0
        }
        nbeams[key] += v
    }
    for bm := beams {
        b := bm.key
        v := bm.value
        if row[b] == 2 {
            incr(b-1, v)
            row[b-1] = 3
            incr(b+1, v)
            row[b+1] = 3
        } else {
            incr(b, v)
            row[b] = 3
        }
    }
    return [nbeams, row]
}

start := input[0]
grid := input[1]
printGrid(grid)

h := len(grid)
beams := {start: 1}
for y := 1:h-1 {
    res := step2(grid[y], beams)
    beams = res[0]
    grid[y] = res[1]
    println("After row ", y, " beams=", beams)
    printGrid(grid)
}

println("Part 1: Total splitters hit:", sum)
paths := 0
for bm := beams {
    paths += bm.value
}
println("Part 2: Total Paths:", paths)
